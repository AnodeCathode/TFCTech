buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

if (System.getenv('VERSION') != null) {
    version = System.getenv('VERSION')
} else {
    version = "1.2.9"
}
group = "tfctech"
archivesBaseName = "TFCTech-Unofficial"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
// Deobf jar allows people to drop this in mod folder on a dev environment
task deobfJar(type: Jar, dependsOn: 'jar') {
    from sourceSets.main.output
    classifier "deobf"
}

artifacts {
    archives deobfJar // Default jars are already in there.
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_39"
	
	replace("@VERSION@",project.version)
    replace("@FINGERPRINT@", project.findProperty("signSHA1"))
    replaceIn("TFCTech.java")
}

repositories {
    // Repositories required for dependencies, not ForgeGradle go here.
    mavenCentral()
    maven {
        name = "CraftTweaker Maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Hwyla maven"
        url "http://tehnut.info/maven"
    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
      name = "ic2"
      url = "http://maven.ic2.player.to/"
    }
    maven {
      url "https://minecraft.curseforge.com/api/maven"
    }
    maven { // ModMaven, there is a bunch of stuff on there
      name 'ModMaven'
      url "http://modmaven.k-4u.nl/"
    }
  
}

dependencies {
    deobfProvided "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.11.505"
    deobfProvided "mezz.jei:jei_1.12.2:4.15.0.291"
    compileOnly "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
    deobfProvided "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.12-6"
    compileOnly "gregtechce:gregtech:1.12.2:1.8.5.436"
    compileOnly 'net.industrial-craft:industrialcraft-2:2.8.170-ex112:api'
    compile fileTree(include: ["*.jar"], dir: "libs")
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	
	// from the project root, include the LICENSE file
    from('.') {
        include "LICENSE.txt"
    }
}

project.tasks.withType(Jar) { jarTask -> // For all jar tasks
    jarTask.appendix = "MC" + project.minecraft.version
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf { // Skip the task if our secret data isn't available
        project.hasProperty('keyStore')
    }

    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar